<program> ===> <otherFunctions> <mainFunction>
<mainFunction> ===> TK_MAIN <stmts> TK_END
<otherFunctions> ===> <function> <otherFunctions>
<otherFunctions> ===> <eps>
<function> ===> TK_FUNID <input_par> <output_par> TK_SEM <stmts> TK_END
<input_par> ===> TK_INPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par> ===> TK_OUTPUT TK_PARAMETER TK_LIST TK_SQL <parameter_list> TK_SQR
<output_par> ===> <eps>
<parameter_list> ===> <dataType> TK_ID <remaining_list>
<dataType> ===> <primitiveDataType>
<dataType> ===> <constructedDataType>
<primitiveDataType> ===> TK_INT
<primitiveDataType> ===> TK_REAL
<constructedDataType> ===> <A> TK_RUID
<constructedDataType> ===> TK_RUID
<remaining_list> ===> TK_COMMA <parameter_list>
<remaining_list> ===> <eps>
<stmts> ===> <typeDefinitions> <declarations> <otherStmts> <returnStmt>
<typeDefinitions> ===> <actualOrRedefined> <typeDefinitions>
<typeDefinitions> ===> <eps>
<actualOrRedefined> ===> <typeDefinition>
<actualOrRedefined> ===> <definetypestmt>
<typeDefinition> ===> TK_RECORD TK_RUID <fieldDefinitions> TK_ENDRECORD
<typeDefinition> ===> TK_UNION TK_RUID <fieldDefinitions> TK_ENDUNION
<fieldDefinitions> ===> <fieldDefinition> <fieldDefinition> <moreFields>
<fieldDefinition> ===> TK_TYPE <fieldType> TK_COLON TK_FIELDID TK_SEM
<fieldType> ===> <primitiveDataType>
<fieldType> ===> <constructedDataType>
<moreFields> ===> <fieldDefinition> <moreFields>
<moreFields> ===> <eps>
<declarations> ===> <declaration> <declarations>
<declarations> ===> <eps>
<declaration> ===> TK_TYPE <dataType> TK_COLON TK_ID <global_or_not> TK_SEM
<global_or_not> ===> TK_COLON TK_GLOBAL
<global_or_not> ===> <eps>
<otherStmts> ===> <stmt> <otherStmts>
<otherStmts> ===> <eps>
<stmt> ===> <assignmentStmt>
<stmt> ===> <iterativeStmt>
<stmt> ===> <conditionalStmt>
<stmt> ===> <ioStmt>
<stmt> ===> <funCallStmt>
<assignmentStmt> ===> <SingleOrRecId> TK_ASSIGNOP <arithmeticExpression> TK_SEM
<SingleOrRecId> ===> TK_ID <option_single_constructed>
<option_single_constructed> ===> <oneExpansion> <moreExpansions>
<option_single_constructed> ===> <eps>
<moreExpansions> ===> <oneExpansion> <moreExpansions>
<moreExpansions> ===> <eps>
<oneExpansion> ===> TK_DOT TK_FIELDID
<funCallStmt> ===> <outputParameters> TK_CALL TK_FUNID TK_WITH TK_PARAMETERS <inputParameters> TK_SEM
<outputParameters> ===> TK_SQL <idList> TK_SQR TK_ASSIGNOP
<outputParameters> ===> <eps>
<inputParameters> ===> TK_SQL <idList> TK_SQR
<iterativeStmt> ===> TK_WHILE TK_OP <booleanExpression> TK_CL <stmt> <otherStmts> TK_ENDWHILE
<conditionalStmt> ===> TK_IF TK_OP <booleanExpression> TK_CL TK_THEN <stmt> <otherStmts> <elsePart>
<elsePart> ===> TK_ELSE <stmt> <otherStmts> TK_ENDIF
<elsePart> ===> TK_ENDIF
<ioStmt> ===> TK_READ TK_OP <var> TK_CL TK_SEM
<ioStmt> ===> TK_WRITE TK_OP <var> TK_CL TK_SEM
<arithmeticExpression> ===> <term> <expPrime>
<expPrime> ===> <lowPrecedenceOp> <term> <expPrime>
<expPrime> ===> <eps>
<term> ===> <factor> <termPrime>
<termPrime> ===> <highPrecedenceOp> <factor> <termPrime>
<termPrime> ===> <eps>
<factor> ===> TK_OP <arithmeticExpression> TK_CL
<factor> ===> <var>
<lowPrecedenceOp> ===> TK_PLUS
<lowPrecedenceOp> ===> TK_MINUS
<highPrecedenceOp> ===> TK_MUL
<highPrecedenceOp> ===> TK_DIV
<booleanExpression> ===> TK_OP <booleanExpression> TK_CL <logicalOp> TK_OP <booleanExpression> TK_CL
<booleanExpression> ===> <var> <relationalOp> <var>
<booleanExpression> ===> TK_NOT TK_OP <booleanExpression> TK_CL
<var> ===> <SingleOrRecId>
<var> ===> TK_NUM
<var> ===> TK_RNUM
<logicalOp> ===> TK_AND
<logicalOp> ===> TK_OR
<relationalOp> ===> TK_LT
<relationalOp> ===> TK_LE
<relationalOp> ===> TK_EQ
<relationalOp> ===> TK_GT
<relationalOp> ===> TK_GE
<relationalOp> ===> TK_NE
<returnStmt> ===> TK_RETURN <optionalReturn> TK_SEM
<optionalReturn> ===> TK_SQL <idList> TK_SQR
<optionalReturn> ===> <eps>
<idList> ===> TK_ID <more_ids>
<more_ids> ===> TK_COMMA <idList>
<more_ids> ===> <eps>
<definetypestmt> ===> TK_DEFINETYPE <A> TK_RUID TK_AS TK_RUID
<A> ===> TK_RECORD
<A> ===> TK_UNION
